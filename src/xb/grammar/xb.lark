block      : stmt* [expr]
p_block    : "(" block ")"

?stmt      : expr ";"

?expr      : if_
           | identifier ":" expr       -> const_decl
           | "let" identifier "=" expr -> var_decl
           | expr "=" expr             -> assign

?if_       : logic
           | "if" p_block expr ["else" expr]

?logic     : compare
           | logic "&&" compare -> and_
           | logic "||" compare -> or_

?compare   : sum
           | compare "==" sum -> eq
           | compare "!=" sum -> neq
           | compare "<" sum  -> lt
           | compare ">" sum  -> gt
           | compare "<=" sum -> lte
           | compare ">=" sum -> gte

?sum       : product
           | sum "+" product -> add
           | sum "-" product -> sub

?product   : pow
           | product "*" pow  -> mul
           | product "/" pow  -> div
           | product "//" pow -> int_div
           | product "%" pow  -> mod

?pow       : coalesce
           | pow "**" coalesce

?coalesce  : unary
           | coalesce "??" unary

?unary     : acc_call
           | "-" unary -> neg
           | "!" unary -> not_

?acc_call  : atom
           | acc_call "." key      -> key_access
           | acc_call "[" expr "]" -> index_access
           | acc_call "(" args ")" -> call

?atom      : literal
           | construct
           | identifier
           | p_block

?construct : array
           | object
           | function

?literal   : number
           | string
           | bool
           | "()"   -> empty

array      : "[" _expr_list "]"
object     : "{" (pair ("," pair)* ","?)? "}"
function   : "fn" "(" params ")" expr

pair       : identifier   -> infer_pair
           | key ":" expr -> const_pair
           | key "=" expr -> var_pair

params  : (identifier ("," identifier)* ","?)?
args    : _expr_list

_expr_list : (expr ("," expr)* ","?)?

identifier : IDENT
key        : IDENT

number     : SIGNED_NUMBER
string     : ESCAPED_STRING
bool       : BOOL

BOOL.1     : "true" | "false"
IDENT.0    : /[a-zA-Z_$][a-zA-Z0-9_$]*/

%import common (SIGNED_NUMBER, ESCAPED_STRING, WS)
%import common.SH_COMMENT -> COMMENT

%extend SIGNED_NUMBER : /[-+]?0[xX][a-fA-F0-9]+/

%ignore COMMENT
%ignore WS
